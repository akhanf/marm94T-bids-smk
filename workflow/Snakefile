from glob import glob
from snakebids import bids

configfile: 'config.yml'

  
rule all:
    input:
        niftis=expand('niftis/sub-{subject}',subject=config['tarfiles'].keys()),
        uni=expand(bids(root='bids',subject='{subject}',acq='mp2rage',datatype='anat',suffix='T1w.nii.gz'),
                subject=config['tarfiles'].keys()),
        dwi=expand(bids(root='bids',subject='{subject}',acq='{acq}',datatype='dwi',suffix='dwi.{ext}'),
                subject=config['tarfiles'].keys(),
                acq=['multishell','revb0'],
                ext=['nii.gz','bval','bvec','json']
            ),
        mtsat=expand(bids(root='bids',subject='{subject}',acq='{acq}',datatype='anat',suffix='MTsat.nii.gz'),
                subject=config['tarfiles'].keys(),
                acq=['MTw','PDw','T1w'],
            ),

        dd='bids/dataset_description.json'
    
rule all_mp2rage:
    input:
         uni=expand(bids(root='bids',subject='{subject}',acq='mp2rage',datatype='anat',suffix='T1w.nii.gz'),
                subject=config['tarfiles'].keys()),
         invs=expand(bids(root='work',subject='{subject}',acq='mp2rage',datatype='anat',part='{part}',inv='{inv}',suffix='MP2RAGE.nii.gz'),
                subject=config['tarfiles'].keys(),
                part=['mag','phase'],
                inv=[1,2]
                ),
      

rule extract_tar:
    input:
        tarfile = lambda wildcards: config['tarfiles'][wildcards.subject]
    output:
        directory('dicoms/sub-{subject}')
    shell: 
        'mkdir -p {output} && tar -C {output} -xvf {input}'

rule dcm_to_nii:
    input:
        'dicoms/sub-{subject}'
    output:
        directory('niftis/sub-{subject}')
    shell:
        "mkdir -p {output} && dcm2niix -f '%s_%d' -o {output} -d 9 {input}"


rule cp_extra_bids:
    input:
        dd='resources/dataset_description.json',
        ignore='resources/bidsignore'
    output:
        dd='bids/dataset_description.json',
        ignore='bids/.bidsignore'
    shell:
        'cp {input.dd} {output.dd} && '
        'cp {input.ignore} {output.ignore}'

include: 'rules/mp2rage.smk'
include: 'rules/dwi.smk'
include: 'rules/mtsat.smk'
