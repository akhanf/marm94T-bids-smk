from glob import glob
from snakebids import bids

configfile: 'config.yml'

rule all:
    input:
        niftis=expand('niftis/sub-{subject}',subject=config['tarfiles'].keys()),
        uni=expand(bids(root='bids',subject='{subject}',acq='mp2rage',datatype='anat',suffix='T1w.nii.gz'),
                subject=config['tarfiles'].keys()),
        dwi=expand(bids(root='bids',subject='{subject}',acq='{acq}',datatype='dwi',suffix='dwi.nii.gz'),
                subject=config['tarfiles'].keys(),
                acq=['multishell','revb0'],
            ),
        mtsat=expand(bids(root='bids',subject='{subject}',acq='{acq}',datatype='anat',suffix='MTsat.nii.gz'),
                subject=config['tarfiles'].keys(),
                acq=['MTw','PDw','T1w'],
            ),

        dd='bids/dataset_description.json'
    
               

rule extract_tar:
    input:
        tarfile = lambda wildcards: config['tarfiles'][wildcards.subject]
    output:
        directory('dicoms/sub-{subject}')
    shell: 
        'mkdir -p {output} && tar -C {output} -xvf {input}'

rule dcm_to_nii:
    input:
        'dicoms/sub-{subject}'
    output:
        directory('niftis/sub-{subject}')
    shell:
        "mkdir -p {output} && dcm2niix -f '%s_%d' -o {output} -d 9 {input}"


rule cp_dd_json:
    input:
        'resources/dataset_description.json'
    output:
        'bids/dataset_description.json'
    shell:
        'cp {input} {output}'

include: 'rules/mp2rage.smk'
include: 'rules/dwi.smk'
include: 'rules/mtsat.smk'
